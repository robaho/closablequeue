plugins {
    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'
    id "me.champeau.jmh" version "0.7.2"
    id 'maven-publish'
    id 'signing'
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation libs.junit.jupiter

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.openjdk.jmh:jmh-core:1.37'
}

// Apply a specific Java toolchain to ease working on different environments.
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}

jmh {
    // includes = [ "testSingleConsumerQueue" ]
    // includes = [ "testClosableQueue" ]
}

publish {
    dependsOn test
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.robaho'
            artifactId = 'closablequeue'
            version = '1.0.8'

            from components.java

            pom {
                name = 'closablequeue'
                description = 'An unbounded FIFO queue with "close" semantics designed for ephemeral virtual threads.'

                signing {
                    sign publishing.publications.maven
                    sign configurations.archives
                }

                url = 'https://github.com/robaho/closablequeue'

                scm {
                    url = 'https://github.com/robaho/closablequeue.git'
                }

                licenses {
                    license {
                        name = 'gnu v2.0'
                        url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.html'
                    }
                }

                developers {
                    developer {
                        id = 'robaho'
                        name = 'Robert Engels'
                        email = 'robaho@me.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
        name = "OSSRH"
        url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = "$maven_user"
                password = "$maven_password"
            }            
        }
    }
}
